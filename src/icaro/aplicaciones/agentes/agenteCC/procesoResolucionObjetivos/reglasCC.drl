import icaro.infraestructura.entidadesBasicas.procesadorCognitivo.InformeDeTarea;
import icaro.infraestructura.recursosOrganizacion.recursoTrazas.ItfUsoRecursoTrazas;
import icaro.infraestructura.recursosOrganizacion.recursoTrazas.imp.componentes.InfoTraza;
import icaro.infraestructura.entidadesBasicas.procesadorCognitivo.*;
import icaro.infraestructura.patronAgenteCognitivo.procesadorObjetivos.gestorTareas.ItfGestorTareas;
import icaro.aplicaciones.SIDEMA.*;
import icaro.infraestructura.entidadesBasicas.procesadorCognitivo.Objetivo;
import icaro.aplicaciones.agentes.agenteCC.tareas.*;
import icaro.aplicaciones.agentes.agenteCC.objetivos.*;
import icaro.aplicaciones.Rosace.informacion.*;
import icaro.aplicaciones.Rosace.tareasComunes.*;
import icaro.aplicaciones.Rosace.objetivosComunes.*;
import icaro.aplicaciones.agentes.agenteAplicacionSubordinadoCognitivo.tareas.*;
import java.lang.String;

global ItfGestorTareas gestorTareas;
global ItfUsoRecursoTrazas recursoTrazas;
global String agentId;

//Primera regla que se ejecutara. Solo se debe disparar una vez.
rule "Acciones Iniciales Centro de Control"
	when
		not (exists(Focus()))
		not (exists(MisObjetivos()))
	then
		TareaSincrona tarea1 = gestorTareas.crearTareaSincrona(InicializarInfoWorkMem.class);
   		tarea1.ejecutar( );
   		//TODO mirar que hace tarea 2
   		TareaSincrona tarea2 = gestorTareas.crearTareaSincrona(InicializarRobotSIDEMA.class);
   		tarea2.ejecutar( );
   		recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," :REGLA " + drools.getRule().getName());
   		System.out.println( "\n"+agentId +" EJECUTO LA REGLA Acciones Iniciales\n\n" ); 
end



rule "Generacion Objetivo ConseguirMapa "
// Se genera al principio de la simulacion 
when
    focoActual : Focus(foco == null)
    misObjs: MisObjetivos()
    not (exists (ConseguirMapa()))
 then
    ConseguirMapa conseguirMapaObj = new ConseguirMapa();
 	//se genera y se focaliza
    TareaSincrona tarea = gestorTareas.crearTareaSincrona(GenerarObjetivoyFocalizarlo.class);
    tarea.ejecutar(conseguirMapaObj,misObjs,focoActual); 
    System.out.println( "\n"+agentId +" EJECUTO LA REGLA: "+drools.getRule().getName()+"\n\n" );
    System.out.println( "\n"+agentId + "Foco: NULL " +" \n\n" );
    recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());     
end

rule "Consecución Mapa Escenario "
// Obtención del escenario de simulación
when   
    miMapa : Mapa(mapa ==null)
    obj1 : ConseguirMapa(state == Objetivo.SOLVING)
    focoActual : Focus(foco == obj1)
then
    TareaSincrona tarea = gestorTareas.crearTareaSincrona(ObtenerMapa.class);
    tarea.ejecutar(); 
    System.out.println( "\n"+agentId +" EJECUTO LA REGLA: "+drools.getRule().getName()+"\n\n" );
    recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());     
end

rule "Comprobar consecución Mapa"
when   
    misObjs: MisObjetivos()
    miMapa: Mapa(mapa!=null)
    obj1 : ConseguirMapa(state == Objetivo.SOLVING)
    focoActual : Focus(foco == obj1)
then
	// se da por conseguido el objetivo 
    TareaSincrona tarea = gestorTareas.crearTareaSincrona(ConseguirObjetivoActualizarFoco.class);
    tarea.ejecutar(misObjs,obj1,focoActual ); 
    System.out.println( "\n"+agentId +" EJECUTO LA REGLA: "+drools.getRule().getName()+"\n\n" );
    recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());     
end

rule "Distribuir Terreno"
//Distribución del terreno entre los distintos agentes
	when
		focoActual : Focus(foco == null)
		obj : ConseguirMapa(state== Objetivo.SOLVED)
		mapa: Mapa()
	then
		TareaSincrona tarea = gestorTareas.crearTareaSincrona(icaro.aplicaciones.agentes.agenteCC.tareas.DistribuirTerreno.class);
		tarea.ejecutar(mapa);
		recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," :REGLA " + drools.getRule().getName());
		System.out.println("\n"+agentId +" EJECUTO LA REGLA Distribuir Terreno\n\n");
end

rule "Solicitar Ubicacion Actual"
	when
		focoActual : Focus(foco == null)
		ordenCC: OrdenMinaEncontrada( mensajeOrden == VocabularioSIDEMA.MsgPeticionMinaEncontrada, minaCelda : justificacion)
		robot: CentroControl(id == agentId)
	then
		TareaSincrona tarea = gestorTareas.crearTareaSincrona(icaro.aplicaciones.agentes.agenteCC.tareas.SolicitarInformacionNeutralizador.class);
		tarea.ejecutar(robot,minaCelda);
		recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," :REGLA " + drools.getRule().getName());
		System.out.println("\n"+agentId +" EJECUTO LA REGLA Solicitar Ubicacion Actual\n\n");
end

rule "Recibir Ubicacion Actual"
	when
		focoActual : Focus(foco == null)
		ordenCC: OrdenInformarPosicionActual( mensajeOrden == VocabularioSIDEMA.MsgInformarPosicionActual, emisor : identEmisor, celdaActual : justificacion)
		robot: CentroControl(id == agentId)
	then
		robot.actualizarMsg((Celda)celdaActual,emisor);
		recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," :REGLA " + drools.getRule().getName());
		System.out.println("\n"+agentId +" EJECUTO LA REGLA Recibir Ubicacion Actual\n\n");
		if(robot.recibidosNeutralizadores()){
			TareaSincrona tarea = gestorTareas.crearTareaSincrona(icaro.aplicaciones.agentes.agenteCC.tareas.EnviarNeutralizador.class);
			tarea.ejecutar(robot);
			recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," :REGLA " + drools.getRule().getName());
			System.out.println("\n"+agentId +" EJECUTO LA REGLA Recibir Ubicación Actual\n\n");
		}
end

rule "Encontrado Neutralizador Libre"
	when
		focoActual : Focus(foco == null)
		ordenCC: OrdenInformarNeutralizadorLibre( mensajeOrden == VocabularioSIDEMA.MsgInformarNeutralizadorLibre, emisor : identEmisor)
		robot: CentroControl(id == agentId)
	then
		if(!robot.getNeutralizadores().contains(emisor))
			robot.getNeutralizadores().add(emisor);
		recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," :REGLA " + drools.getRule().getName());
		System.out.println("\n"+agentId +" EJECUTO LA REGLA Encontrado Neutralizador Libre \n\n");
		if(robot.recibidosNeutralizadores()){
			TareaSincrona tarea = gestorTareas.crearTareaSincrona(icaro.aplicaciones.agentes.agenteCC.tareas.EnviarNeutralizador.class);
			tarea.ejecutar(robot);
			recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," :REGLA " + drools.getRule().getName());
			System.out.println("\n"+agentId +" EJECUTO LA REGLA Encontrado Neutralizador Libre\n\n");
		}
end


rule "Enviar Neutralizador"
	when
		focoActual : Focus(foco == null)
		robot : CentroControl(id == agentId)
		eval(robot.recibidosNeutralizadores())
	then
		TareaSincrona tarea = gestorTareas.crearTareaSincrona(icaro.aplicaciones.agentes.agenteCC.tareas.EnviarNeutralizador.class);
		tarea.ejecutar(robot);
		recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," :REGLA " + drools.getRule().getName());
		System.out.println("\n"+agentId +" EJECUTO LA REGLA Enviar Neutralizador\n\n");
end
